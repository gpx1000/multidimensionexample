import java.text.SimpleDateFormat

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0-beta1'
        classpath 'com.android.tools.build:gradle-experimental:0.8.0-beta1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def getBuildNumber() {
    return System.getenv("BUILD_NUMBER") as Integer ?: 0
}

def buildTime() {
    def df = new SimpleDateFormat("dd-MM-yyyy HH:mm")
    df.setTimeZone(TimeZone.getTimeZone("UTC+01:00"))
    return df.format(new Date())
}

ext.systemProperties = System.properties;

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

project.ext {
    compileSdkVersion = 24
    buildToolsVersion = "24.0.1"
    stlVersion = "c++_shared"
    versionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild + getBuildNumber()
    versionName = "ver-${versionMajor}.${versionMinor}.${versionPatch} ${buildTime()}"
    minSdkVersion = 21
    platformVersion = "android-21"
    targetSdkVersion = 24
    ndkDir = properties.getProperty('ndk.dir')
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
